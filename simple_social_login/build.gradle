plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('social.properties').newDataInputStream())

def kakao_sdk_appkey = properties.getProperty('kakao.sdk.appkey', null)
def kakao_sdk_nativekey = properties.getProperty('kakao.sdk.nativekey', null)

android {
    compileSdk 32

    defaultConfig {
        minSdk 26
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        manifestPlaceholders = [
                kakaoAppKey: kakao_sdk_appkey,
                kakaoNativeKey: kakao_sdk_nativekey
        ]
        buildConfigField "String", "KAKAO_APP_KEY", kakao_sdk_appkey
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {

    def lifecycle = "2.4.1"
    def coroutines = "1.6.1"
    implementation 'androidx.activity:activity-ktx:1.6.0-alpha03'
    implementation 'androidx.fragment:fragment-ktx:1.5.0-beta01'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    // K A K A O
    def kakao = "2.9.0"
    def gson = "2.8.6"
    implementation "com.kakao.sdk:v2-user:$kakao" // kakao login
    implementation "com.google.code.gson:gson:$gson" // kakao proguard rule 적용을 위함.

    // G O O G L E
    def firebaseBom = "29.3.1"
    def playServiceAuth = "20.2.0"
    implementation platform("com.google.firebase:firebase-bom:$firebaseBom")
    implementation "com.google.firebase:firebase-auth-ktx"
    implementation "com.google.android.gms:play-services-auth:$playServiceAuth"

}
